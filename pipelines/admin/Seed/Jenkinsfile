import javaposse.jobdsl.plugin.LookupStrategy


withNode {
    stage('checkout') {
        checkout scm
    }
    stage('seed') {
        seedJob getDslFiles().join('\n')
    }
}

void withNode(Closure body) {
    node getNodeName(), {
        ansiColor 'xterm', body
    }
}

List<String> getDslFiles() {
    findFiles(glob: "**/Jenkinsfile.dsl")
        .findAll { !it.directory }
        .collect { it.path }
        .toSorted()
}

void seedJob(String dslFileGlob) {
    catchError(buildResult: 'FAILURE', stageResult: 'FAILURE') {
        jobDsl targets: dslFileGlob,
            lookupStrategy: getLookupStrategy(),
            ignoreExisting: false,
            ignoreMissingFiles: true,
            removedJobAction: 'DISABLE',
            removedConfigFilesAction: 'IGNORE',
            removedViewAction: 'DELETE',
            unstableOnDeprecation: true,
            additionalClasspath: 'src',
            additionalParameters: [
                WORKSPACE: env.WORKSPACE,
                REPO_ROOT_DIR : env.WORKSPACE,
            ]
    }
}

String getLookupStrategy() {
    if (LookupStrategy.values().any { params.LOOKUP_STRATEGY == it.name() }) {
        return params.LOOKUP_STRATEGY
    }
    'SEED_JOB'
}

String getNodeName() {
    env.NODE_LABEL ?: 'admin'
}
