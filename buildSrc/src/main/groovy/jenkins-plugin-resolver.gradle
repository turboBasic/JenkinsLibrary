import static org.example.JenkinsPlugins.jenkinsVersion
import static org.example.JenkinsPlugins.testHarnessVersion
import org.example.JenkinsPlugins


plugins {
    id 'groovy'
    id 'idea'
    id 'com.adarshr.test-logger'
    id 'com.dorongold.task-tree'
}

java {
    toolchain {
        setLanguageVersion JavaLanguageVersion.of(11)
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    JenkinsPlugins.tearDown()
    JenkinsPlugins.create(project)

    testPlugins {}
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    /*  Project's Shared library code dependencies */
    implementation 'org.codehaus.groovy:groovy-all:2.4.21'
    implementation "org.jenkins-ci.main:jenkins-core:$jenkinsVersion"
    implementationDependencies delegate

    /*  JUnit5 dependencies */
    testImplementation platform('org.junit:junit-bom:5.9.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'

    /*  Jenkins plugins dependencies */
    JenkinsPlugins.get().each { String dependency ->
        testPlugins dependency
    }

    /*  Dependencies of tests of Shared library and pipeline code */
    testImplementation 'com.lesfurets:jenkins-pipeline-unit:1.19'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.4'
    testImplementationDependencies delegate

    /*  Dependencies of integration tests */
    integrationTestImplementation "org.jenkins-ci.main:jenkins-test-harness:$testHarnessVersion"
    integrationTestImplementation "org.jenkins-ci.main:jenkins-war:$jenkinsVersion"
}

tasks.named('test') {
    inputs.files sourceSets.main.allSource.files

    final String STACK_WRITE_PROPERTY = 'pipeline.stack.write'
    final String LOGLEVEL_PROPERTY = 'testlogger.logLevel'

    systemProperty STACK_WRITE_PROPERTY, System.getProperty(STACK_WRITE_PROPERTY) ?: findProperty(STACK_WRITE_PROPERTY)
    systemProperty LOGLEVEL_PROPERTY, System.getProperty(LOGLEVEL_PROPERTY) ?: findProperty(LOGLEVEL_PROPERTY)

    useJUnitPlatform()

    testlogger {
        theme 'mocha-parallel'
        showStandardStreams true
        showFullStackTraces true
    }

    reports {
        junitXml {
            outputPerTestCase = true
        }
    }
}

tasks.register('resolveTestPlugins', Copy) {
    group 'verification'
    description 'Collect Jenkins plugins and make them available for Jenkins Test Harness'

    final String destinationDir = "${sourceSets.integrationTest.output.resourcesDir}/test-dependencies"

    doFirst {
        Set<ResolvedArtifact> resolvedArtifacts = configurations.testPlugins
            .resolvedConfiguration
            .resolvedArtifacts
        JenkinsPlugins.get().registerResolvedPlugins(resolvedArtifacts)
    }

    from configurations.testPlugins {
        rename { String filePath ->
            JenkinsPlugins.get().pluginFileNameForJenkinsInstance(filePath)
        }
    }
    include '*.hpi', '*.jpi'
    into destinationDir

    doLast {
        JenkinsPlugins.get().generatePluginsIndex("${destinationDir}/index")
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests'
    group = 'verification'

    dependsOn tasks.resolveTestPlugins
    shouldRunAfter test

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    final String JENKINS_TEST_HARNESS_BUILD_DIR_PROPERTY = 'buildDirectory'
    systemProperty JENKINS_TEST_HARNESS_BUILD_DIR_PROPERTY, layout.buildDirectory.get().asFile.toPath().toString()

    useJUnitPlatform()

    testlogger {
        theme 'mocha-parallel'
        showStandardStreams true
        showFullStackTraces true
    }
}

tasks.named('check') {
    dependsOn integrationTest
}

tasks.register('tearDown') {
    group 'Build Setup'
    doLast {
        JenkinsPlugins.tearDown()
    }
}

tasks.findAll { it.group in ['build', 'verification'] }. each {
    it.finalizedBy 'tearDown'
}

tasks.named('wrapper') {
    gradleVersion = project.gradleVersion
    distributionType = Wrapper.DistributionType.BIN
}


/*  Dependency helpers */

static void implementationDependencies(DependencyHandler context) {
    List<String> dependencies = [
        JenkinsPlugins.get()['job-dsl-core'],
    ]

    switch (jenkinsVersion) {
        case '2.277.4':
            dependencies << 'com.cloudbees:groovy-cps:1.22'
            break
        case '2.346.3':
            dependencies << JenkinsPlugins.get()['workflow-cps']
            break
        default:
            throw new IllegalArgumentException("Unrecognized Jenkins version: $jenkinsVersion")
    }

    dependencies.each { String dependency ->
        context.implementation dependency
    }
}

static void testImplementationDependencies(DependencyHandler context) {
    List<String> dependencies = [
        JenkinsPlugins.get()['job-dsl'],
        "${JenkinsPlugins.get()['job-dsl']}@jar",
        "${JenkinsPlugins.get()['structs']}@jar",
    ]

    switch (jenkinsVersion) {
        case '2.277.4':
            dependencies << 'cglib:cglib-nodep:2.2.2'
            dependencies << "${JenkinsPlugins.get()['script-security']}@jar"
            break
        case '2.346.3':
            break
        default:
            throw new IllegalArgumentException("Unrecognized Jenkins version: $jenkinsVersion")
    }

    dependencies.each { String dependency ->
        context.testImplementation dependency
    }
}
